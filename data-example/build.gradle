apply plugin: 'com.android.application'
apply from: "${project.rootDir}/quality.gradle"

static def focusMode() {
    String focusMode = System.getenv("FOCUS_MODE")
    if (focusMode == null) {
        focusMode = "IGNORED"
    }

    return "\"$focusMode\""
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.bose.ar.DataExample"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "FOCUS_MODE", focusMode()
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = outputFileName.replace(".apk", "-${variant.versionCode}.apk")
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

dependencies {
    def nav_version = '2.1.0'

    implementation "com.github.Bose.BoseWearable-Android:bosewearable:$rootProject.ext.bose_ar_version"
    implementation "com.github.Bose.BoseWearable-Android:bosewearableui:$rootProject.ext.bose_ar_version"

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'com.google.android.material:material:1.2.0-alpha02'

    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.legacy:legacy-support-core-ui:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0'

    implementation 'androidx.preference:preference:1.1.0'

    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
